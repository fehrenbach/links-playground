# Links port of the XPath code from
#
#   A Practical Theory of Language-Integrated Query
#   James Cheney, Sam Lindley, Philip Wadler
#   at ICFP'13

var db = database "stefan";

var xml = table ("xml") with
    (id : Int,
     parent : Int,
     name : String,
     pre : Int,
     post : Int) from db;

#typename QueryBuilder = ((pre: Int, post: Int)) {}-> [(|a::(Any,Base))];

typename Axis = [| Self
                 | Child
                 | Descendant
                 | DescendantOrSelf
                 | Following
                 | FollowingSibling
#                 | Rev:Axis # Can't do this easily because recursion is wild, even though in this case it should be very easy.
                 |];

typename Path = [| Axis:Axis
                 | Seq:(Path, Path)
                 #| Name:String
                 #| Filter:Path
                 |];

typename XML = (id:Int,name:String,parent:Int,post:Int,pre:Int);

fun axis(ax, s, t) {
  switch (ax) {
    case Self -> s.id == t.id
    case Child -> s.id == t.parent
    case Descendant -> s.pre < t.pre && t.post < s.post
    case DescendantOrSelf -> s.pre <= t.pre && t.post <= s.post
    case Following -> s.post < t.pre
    case FollowingSibling -> s.post < t.pre && s.parent == t.parent
#    case Rev(ax) -> axis(ax, t, s)
  }
}

#sig path : (Path, QueryBuilder) ~> QueryBuilder
sig path : (Path, (XML) {}-> [XML]) ~> (XML) {}-> [XML]
fun path(p, k) {
  switch (p) {
    case Seq(p, q) ->
      # Do the recursion outside to obtain a non-wild query function (note the {}-> type).
      var pq = path(p, path(q, k)) : ((XML) {}-> [XML]);
      # (Textually) substituting pq in the returned function for the right hand side does not work!
      fun (outer_context) {
          pq(outer_context)
      }
    case Axis(ax) -> fun (p) {
      for (c <-- xml)
      where (axis(ax, p, c))
        k(c)
    }
  }
}

var xp7 = Seq(Axis(Child), Seq(Axis(Child), Axis(Child)));

var xp0 = Seq(Axis(Child), Axis(Child));

var cf = path(Axis(Child), fun (x) { [x] });

var cf0 = path(xp7, fun (x) {[x]});

query { for (r <-- xml)
        where (r.parent == -1)
        cf0(r) }
